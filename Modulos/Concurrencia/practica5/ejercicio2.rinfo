programa ejercicio2
procesos
  proceso tomarFlores(ES cant:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      cant:= cant + 1
  fin
  proceso tomarPapeles
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
  fin
  proceso depositarCantidades
  comenzar
    mientras((HayFlorEnLaBolsa) | (HayPapelEnLaBolsa))
      si(HayFlorEnLaBolsa)
        depositarFlor
      si(HayPapelEnLaBolsa)
        depositarPapel
  fin
  proceso enviarTerminado(E id:numero)
  comenzar
    si (id = 1)
      EnviarMensaje(V,r2)
      EnviarMensaje(V,r3)
    sino 
      si(id = 2)
        EnviarMensaje(V, r1)
        EnviarMensaje(V, r3)
      sino
        EnviarMensaje(V,r1)
        EnviarMensaje(V,r2)
  fin
  proceso recolectarFloresEsquina(E cant:numero)
  comenzar
    repetir cant
      tomarFlor
  fin
  
areas
  aR1: AreaP(1,1,100,1)
  aR2: AreaP(1,2,100,2)
  aR3: AreaP(1,3,100,3)
  areaD: AreaPC(50,50,50,50)
  aJ: AreaP(1,4,1,4)
robots
  robot recolector
  variables
    caOrigen,avOrigen,id,cant:numero
    ok:boolean
  comenzar
    derecha
    cant:= 0
    caOrigen:= PosCa
    RecibirMensaje(id,rJ)
    mientras(PosAv<91)
      repetir 10
        tomarPapeles
        tomarFlores(cant)
        mover
      avOrigen:= PosAv
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarCantidades
      Pos(avOrigen,caOrigen)
      LiberarEsquina(50,50)
      enviarTerminado(id)
      repetir 2
        RecibirMensaje(ok,*)
    repetir 9
      tomarPapeles
      tomarFlores(cant)
      mover
    tomarFlores(cant)
    tomarPapeles
    avOrigen:= PosAv
    BloquearEsquina(50,50)
    Pos(50,50)
    depositarCantidades
    Pos(avOrigen,caOrigen)
    LiberarEsquina(50,50)
    enviarTerminado(id)
    repetir 2
      RecibirMensaje(ok,*)
    EnviarMensaje(cant,rJ)
  fin
  robot jefe
  variables
    cant,aux: numero
  comenzar
    aux:= 0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    repetir 3
      RecibirMensaje(cant,*)
      aux:= aux + cant
    Pos(50,50)
    recolectarFloresEsquina(cant)
    Informar(cant)
  fin
  
variables
  r1: recolector
  r2: recolector
  r3: recolector
  rJ: jefe
comenzar
  AsignarArea(r1,aR1)
  AsignarArea(r2,aR2)
  AsignarArea(r3,aR3)
  AsignarArea(rJ, aJ)
  AsignarArea(r1,areaD)
  AsignarArea(r2,areaD)
  AsignarArea(r3,areaD)
  AsignarArea(rJ,areaD)
  Iniciar(r1,1,1)
  Iniciar(r2,1,2)
  Iniciar(r3,1,3)
  Iniciar(rJ,1,4)
fin
